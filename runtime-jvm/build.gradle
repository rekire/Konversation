import java.nio.file.Paths

plugins {
    id 'kotlin-platform-jvm'
    // FIX COMPILE ERROR WITH THIS
    //id 'org.jetbrains.kotlin.kapt' version '1.3.11'
    id 'application'
    id 'maven-publish'
}
repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    expectedBy project(":runtime-shared")
    expectedBy project(":shared")
    compile project(":shared")
    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
    // FIX COMPILE ERROR WITH THIS
    //kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'
    compile 'com.squareup.moshi:moshi-kotlin:1.8.0'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'

    // FIX COMPILE ERROR WITH THIS
    //main.java.srcDirs += file("$buildDir/stubs")

    // For kotlin code gen during kapt
    main.java.srcDirs += [file("$buildDir/generated/source/kaptKotlin/main")]
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
sourceCompatibility = "1.8"


task buildPreviewJar(type: Jar) {
    group = 'build'
    manifest.attributes 'Main-Class': 'org.rewedigital.konversation.Shell'
    archiveFileName = "konversation-bundle.jar"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
mainClassName = 'org.rewedigital.konversation.Shell'
run.main = 'org.rewedigital.konversation.Shell'

task copyStubs(type: Copy) {
    from file("$rootDir/shared/src/main/kotlin/org/rewedigital/konversation/")
    include "**/Part*.kt"
    into file("$buildDir/stubs/org/rewedigital/konversation/")
}
build.dependsOn copyStubs

apply from: '../docu.gradle'
dokka {
    impliedPlatforms = ["JVM"] // This will force platform tags for all non-common sources e.g. "JVM"
    kotlinTasks {
        // dokka fails to retrieve sources from MPP-tasks so they must be set empty to avoid exception
        // use sourceRoot instead (see below)
        []
    }
    sourceRoot {
        // assuming there is only a single source dir...
        path = kotlin.sourceSets.main.kotlin.srcDirs[0]
        platforms = ["JVM"]
    }
    externalDocumentationLink {
        url = new URL("https://github.com/rewe-digital-incubator/${rootProject.name}/blob/master/docs/shared/")
        packageListUrl = Paths.get("$rootDir/docs/shared/package-list").toUri().toURL()
    }
}

publishing.publications {
    maven(MavenPublication) {
        groupId = 'org.rewedigital'
        artifactId = 'konversation'
        version = '0.1'

        from components.java

        // remove internal dependencies they are included in the output jar
        pom.withXml {
            asNode().dependencies.dependency.each { dep ->
                if (dep.artifactId.last().value().last() in ["runtime-shared", "shared"]) {
                    assert dep.parent().remove(dep)
                }
            }
        }
    }
}