{
  parserClass='org.rewedigital.konversation.editor.KonversationParser'
  parserUtilClass='org.rewedigital.konversation.editor.KonversationParserUtil'

  extends='com.intellij.extapi.psi.ASTWrapperPsiElement'

  psiClassPrefix='Konversation'
  psiImplClassSuffix='Impl'
  psiPackage='org.rewedigital.konversation.editor.psi'
  psiImplPackage='org.rewedigital.konversation.editor.impl'

  elementTypeHolderClass='org.rewedigital.konversation.editor.psi.KonversationTypes'
  elementTypeClass='org.rewedigital.konversation.editor.psi.KonversationElementType'
  tokenTypeClass='org.rewedigital.konversation.editor.psi.KonversationElementType'

  tokens = [
      ANNOTATION='@'
      COLON=':'
      UTTERANCE='!'
      BLOCK='-'
      VOICE_ONLY_BLOCK='~'
      BLOCK_CONCAT='+'
      LEFT_BRACE='{'
      RIGHT_BRACE='}'
      SUGGESTION_START='['
      SUGGESTION_END=']'
      VARIABLE_DOLLAR='$'
      VARIABLE_PERCENT='%'
      SHARP_COMMENT='#'
      SLASH_COMMENT='//'

      string='regexp:[^\r\n:!+~\[\]?@/#-]+'
      REPROMPT_MARKER='regexp:\?[1-3]'
      WHITE_SPACE = 'regexp:\s+'
  ]
}

file ::= commandDelimitter* (intentBlock commandDelimitter*)*
intentBlock ::= intentDeclaration utterancesBlock promptBlock repromptBlock suggestionLine
annotations ::= ANNOTATION line commandDelimitter
intentDeclaration ::= annotations* intentName COLON commandDelimitter
commandDelimitter ::= comment? WHITE_SPACE
intentName ::= intentNameChars {mixin='org.rewedigital.konversation.editor.psi.impl.KonversationNamedElementImpl'
                             implements='org.rewedigital.konversation.editor.psi.KonversationNamedElement' methods=[getKey getValue getName setName getNameIdentifier]}
intentNameChars ::= (string BLOCK | string)+
Reprompt ::= line
TextPrompt ::= line
VoicePrompt ::= line
utterence ::= line
suggestion ::= line
utterancesBlock ::= utteranceLine*
utteranceLine ::= '!' utterence commandDelimitter
textBlock ::= ('-' TextPrompt commandDelimitter)+
voiceBlock ::= ('~' VoicePrompt commandDelimitter)+
concatLine ::= '+' commandDelimitter
concatLineBreak ::= '-' commandDelimitter
blockPart ::= textBlock | voiceBlock | concatLine | concatLineBreak | commandDelimitter
promptBlock ::= blockPart*
repromptBlock ::= (repromptLine commandDelimitter)*
repromptLine ::= REPROMPT_MARKER Reprompt
suggestionLine ::= (' '* suggestionBlock)*
suggestionBlock ::= SUGGESTION_START suggestion SUGGESTION_END

line ::= (string '@' | string ':' | string '!' | string '+' | string '-' | string '?' | string '/' | string)+
comment ::= (SHARP_COMMENT | SLASH_COMMENT) (ANNOTATION | COLON | UTTERANCE | BLOCK | VOICE_ONLY_BLOCK | BLOCK_CONCAT | LEFT_BRACE | RIGHT_BRACE | SUGGESTION_START | SUGGESTION_END | VARIABLE_DOLLAR | VARIABLE_PERCENT | SHARP_COMMENT | SLASH_COMMENT | REPROMPT_MARKER | '?' | line)+