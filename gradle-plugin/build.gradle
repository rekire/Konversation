plugins {
    id 'kotlin-platform-jvm'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
}

apply plugin: 'kotlin'

group 'org.rewedigital.konversation'
version rootProject.ext.versions.gradle_plugin

configurations {
    embed
}

dependencies {
    compileOnly project(':cli')
    embed project(':cli')
    implementation gradleApi()
    implementation localGroovy()
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.7'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.12'
}

pluginBundle {
    website = 'https://rewe-digital.github.io/Konversation'
    vcsUrl = 'https://github.com/rewe-digital/Konversation'

    plugins {
        konversation {
            displayName = 'Konversation compiler'
            description = 'Konversation is a tool to generate rich and diversified responses to the user of voice applications. This gradle plugin will compile your ksv files to kson files and add it to your resource for the runtime library.'
            tags = ['konversation', 'voice', 'alexa', 'google assistant']
        }
    }
}

gradlePlugin {
    plugins {
        konversation {
            id = 'org.rewedigital.konversation'
            description = 'Konversation is a tool to generate rich and diversified responses to the user of voice applications. This gradle plugin will compile your ksv files to kson files and add it to your resource for the runtime library.'
            implementationClass = 'org.rewedigital.konversation.KonversationPlugin'
        }
    }
}

repositories {
    jcenter()
    mavenLocal()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
}

jar {
    dependsOn configurations.embed
    from { configurations.embed.collect { zipTree(it) } }
}